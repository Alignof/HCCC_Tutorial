# ラベルとループ

さて，実用的なプログラムを書くためには，条件分岐やループがないと生きていけませんね．

ということで，このチュートリアルの締めとして，それらをアセンブリで書く方法について説明していきます．

## ラベル

今回重要になってくるのは，[section2: アセンブリの基礎](/sections/section2_BasicOfAssembly.md) でチラッと登場した「ラベル」という概念です．

section 2 で登場したアセンブリをもう一度見てみましょう．

```
.intel_syntax noprefix
.globl main

main:
    push    rbp
    mov     rbp, rsp
    mov     eax, 0
    pop     rbp
    ret
```

`main:` と書くことで `main` という名前のラベルを作り，そしてそれを外部から参照するために `.globl main` をつけていたのでしたね．
このように，ラベルを使うことでアセンブリ内の位置を参照できるようになります．
mainラベルを置くことでcallを使ってmain関数（の書かれているアセンブリ群の先頭）に飛ぶことができるのも
ラベルという機能があるおかげなのです．

## ジャンプ命令とループ

「書いてきた命令が上から下に順次実行されていく」というのを，いままで当たり前のこととして解説してきました．

これがなぜできるかというと，CPU の中に存在する「指」[^1]が，常に「次はこの命令を実行してやるぞ」と直後の命令を指差しているからです．

[^1]: 「プログラムカウンタ」(PC)や，「インストラクションポインタ」(IP)などと呼ばれます．

ということで，この「指」を無理やりつかんで，別のところを指差すようにしてしまえば，「次はこの命令を実行してやるぞ」という位置が変わる，ということです．

<!-- さて，大抵のアーキテクチャにはジャンプ命令（もしくはブランチ命令）が存在します．
コンピュータはPC（プログラムカウンタ）が指している番地に存在するデータを命令と解釈して順に実行しますが，
ジャンプ命令はこのPCを今の値から増減させたり，ある値にセットしたりすることで実行する命令を変更します．-->

ジャンプ命令は，まさにこのための命令で，ラベルを使って「指」を動かし，「次はこの位置から命令を実行していってください」というのを可能にします．

言い換えれば，ラベルを使ってアセンブリ内の所望の位置に飛ぶことができるということですが，
これは順方向に飛ぶことで後に続く処理をスキップしたり，逆方向に飛ぶことでループを実現したりすることができるということです．

単純な例を示してみます．
```
.intel_syntax noprefix
.globl main

main:
    push    rbp
    mov     rbp, rsp

LOOP:
    jmp LOOP

    mov     eax, 0

    pop     rbp
    ret
```

処理は上から下に進みますが，飛ぶ先のラベルである`LOOP`はjmp命令の前にあるのでこれは逆方向のジャンプです．
jmp命令までたどり着くとjmp命令の前に飛びます．つまり，これは無限ループを表しています．

無限ループが実現できるようになったのは大きな進歩ですが，これではいつまで経ってもプログラムが終了しません．
ここで今度は順方向のジャンプを使います．
一定の条件を満たしたときにjmp命令の後ろに飛ぶようにすれば，ループから抜け出すことができますね．
ちょうど以下のような感じです．

```
.intel_syntax noprefix
.globl main

main:
    push    rbp
    mov     rbp, rsp

    mov     eax, 0

LOOP:
    cmp eax, 5
    je LOOP_END

    inc eax
    jmp LOOP
LOOP_END:

    pop     rbp
    ret
```

`LOOP`というラベルの他に`LOOP_END`というラベルが置かれているのが分かると思います．
`LOOP_END`はループの先頭に戻る`jmp LOOP`の後に書かれているのでここに飛べば，無限ループから抜け出せるはずです．
ちなみに上のコードは`eax`が0から始まって5になるまで1つずつ値を足していく繰り返し処理になっています．

このように，逆方向へのジャンプによる無限ループと順方向へのジャンプによるループからの脱出を組み合わせることで，
whileやforなどの繰り返し処理は書かれているのです．

では，どうやって条件をつけてジャンプをするのか？
それは次のセクションで．

# 次のセクション
[section9: 条件分岐](/sections/section9_Branching.md)

